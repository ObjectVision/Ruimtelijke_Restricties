container Ruimtelijke_Restricties
{
	container ModelParameters
	{
		parameter<string> RegioIndelingen_jaar := '2022';
		// parameter<string> Export_Date          := '20240806';
		
		parameter<string> CurrentDateTime      := expand(., '%DateTime%');
	}
	
	container Units
	{
		unit<Float32> Meter := BaseUnit('m', float32);
		unit<Float32> Meter2 := Meter * Meter;
	}
	
	container Classifications
	{
		unit<uint8> Restrictie_HardheidK : NrOfRows = 4
		{
			attribute<string> name : ['ZeerHard','Hard','Middel','Zacht'];
		}
		unit<uint8> Stimuli_HardheidK : NrOfRows = 3
		{
			attribute<string> name : ['Hard','Middel','Zacht'];
		}
		
		unit<uint32> Sector_x_ResHardheidK := combine(Actoren/Sector,Restrictie_HardheidK)
		{
			attribute<string> Sector_name     := Actoren/Sector/name[first_rel];
			attribute<string> HardheidK_name  := Restrictie_HardheidK/name[second_rel];
			attribute<string> name            := Sector_name+'_'+HardheidK_name;
			attribute<string> label           := Sector_name+'/'+HardheidK_name;
		}
	
		container Actoren
		{
			unit<uint8> Sector : NrOfRows = 5
			{
				attribute<string> name               : ['Wonen', 'Werken', 'Verblijfsrecreatie', 'Wind', 'Zon'];
				attribute<string> ref                : ['Woongebied', 'Bedrijventerrein', 'Verblijfsrecreatie', 'Wind', 'Zon'];
				attribute<string> Label              := name;
				attribute<uint32> BrushColor         : [rgb(255,60,60), rgb(255,149,255),rgb(255,150,150), rgb(127,127,127), rgb(255,255,0)];
				
				container V := for_each_nedv(name, String(id(.))+'[..]', void, .);
			}
		}	
	}
	
	#include<Geography.dms>
	#include<SourceData.dms>
	#include<Preprocessing.dms>
	
	container Restricties := 
		for_each_ne(
			Classifications/Restrictie_HardheidK/name
			, 'Templates/Restricties_T(SourceData/Beleid,'+quote(Classifications/Restrictie_HardheidK/name)+')'
		), using = "units;Classifications;Geography"
	{
		parameter<string> Generate_All  := 'Ready', ExplicitSuppliers = "=Aslist(Classifications/Restrictie_HardheidK/name+'/Totaal',';')";
		// attribute<bool>   Any (rdc_25m) := =Aslist(Classifications/Restrictie_HardheidK/name+'/Totaal/Any',' || ');
	}
	
	container Stimuli := 
		for_each_ne(
			Classifications/Stimuli_HardheidK/name
			, 'Templates/Stimuli_T(SourceData/Beleid,'+quote(Classifications/Stimuli_HardheidK/name)+')'
		), using = "units;Classifications;Geography"
	{
		parameter<string> Generate_All  := 'Ready', ExplicitSuppliers = "=Aslist(Classifications/Stimuli_HardheidK/name+'/Totaal',';')";
		// attribute<bool>   Any (rdc_25m) := =Aslist(Classifications/Stimuli_HardheidK/name+'/Totaal/Any',' || ');
	}
	
	#include<Templates.dms>
	
	// Settings
	#include <ConfigSettings.dms>
	#include <ExportSettings.dms>
	
}