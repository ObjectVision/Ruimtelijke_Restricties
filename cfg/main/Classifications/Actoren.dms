container Actoren
{
	unit<uint8> Sector : NrOfRows = 5
	{
		attribute<string> name               : ['Wonen', 'Werken', 'Verblijfsrecreatie', 'Wind', 'Zon'];
		attribute<string> ref                : ['Woongebied', 'Bedrijventerrein', 'Verblijfsrecreatie', 'Wind', 'Zon'];
		attribute<string> Label              := name;
		attribute<string> SrcRefRestricties  : ['Woongebied','Bedrijventerrein','Verblijfsrecreatiegebied','Wind','Zon'];
		attribute<string> SrcRefGeschiktheid : ['Wonen/Varianten/@V@/@SS@', 'Werken/Varianten/@V@/@SS@', 'Verblijfsrecreatie/Varianten/@V@/@SS@', 'Wind/Varianten/@V@/@SS@', 'Zon/Varianten/@V@/@SS@'];
		attribute<string> Subsector_ref      : ['Classifications/Vastgoed/WP2xVSSH','Classifications/Actor/Jobs6','Classifications/Vastgoed/VerblijfsrecreatieK','Classifications/Actor/WindK','Classifications/Actor/ZonK'];
		attribute<string> ValUnit_ref        : ['Woning','Job','Verblijfsobject','MW','MW'];
		attribute<string> ValUnitDens_ref    : ['Woning_ha','Job_ha','vbo_ha','MW_ha','MW_ha'];
		attribute<string> ValUnit_ref2       : ['Woningen', 'Banen', 'Woningen', 'Wind', 'Zon']; 
		attribute<uint32> BrushColor         : [rgb(255,60,60), rgb(255,149,255),rgb(255,150,150), rgb(127,127,127), rgb(255,255,0)];
		
		container V := for_each_nedv(name, String(id(.))+'[..]', void, .);
		
		container XSubSectoren := for_each_ne(name, 'Templates/SectorXSubSector('+quote(name)+','+quote(Subsector_ref)+')');
		
		unit<uint32> XSubsector := = 'union_unit('+AsItemList('XSubSectoren/'+Sector/name+'/Subsector')+')'
		{
			attribute<Sector> Sector_rel           := rlookup(Sector_name, Sector/name);
			attribute<string> Sector_name          := = 'union_data(.,'+AsItemList('XSubSectoren/'+Sector/name+'/Subsector/Sector_name')+')';
			attribute<string> SubSector_name       := = 'union_data(.,'+AsItemList('XSubSectoren/'+Sector/name+'/Subsector/subsector_name')+')';
			attribute<string> Subsector_ref        := = 'union_data(.,'+AsItemList('XSubSectoren/'+Sector/name+'/Subsector/Subsector_ref')+')';
			attribute<string> ValUnit_ref          := = 'union_data(.,'+AsItemList('XSubSectoren/'+Sector/name+'/Subsector/ValUnit_ref')+')';
			attribute<string> ValUnitDens_ref      := = 'union_data(.,'+AsItemList('XSubSectoren/'+Sector/name+'/Subsector/ValUnitDens_ref')+')';
			attribute<string> name                 := = 'union_data(.,'+AsItemList('XSubSectoren/'+Sector/name+'/Subsector/name')+')';
			attribute<string> name_totaal          := sector_name == 'Wonen' ? name+'/totaal' : name;
			attribute<string> label                := = 'union_data(.,'+AsItemList('XSubSectoren/'+Sector/name+'/Subsector/label')+')';
			// attribute<Grondgebruik/CBSKlasse/Aggr> CBSKlasse_Aggr_rel := = 'union_data(.,'+AsItemList('XSubSectoren/'+Sector/name+'/Subsector/CBSKlasse_Aggr_rel')+')';
			attribute<uint32> BrushColor           := = 'union_data(.,'+AsItemList('XSubSectoren/'+Sector/name+'/Subsector/BrushColor')+')', DialogType = "BrushColor";
			attribute<string> SrcRefGeschiktheid   := = 'union_data(.,'+AsItemList('XSubSectoren/'+Sector/name+'/Subsector/SrcRefGeschiktheid')+')';
			attribute<string> SrcRefRestricties    := = 'union_data(.,'+AsItemList('Sector/XSubSectoren/'+Sector/name+'/Subsector/SrcRefRestricties')+')';
			attribute<string> ClaimRegio           := rjoin(Sector_name, CaseClassifications/Actor/SectorAllocRegio/Sector_name, CaseClassifications/Actor/SectorAllocRegio/name);
			attribute<string> ClaimRegio_name      := rjoin(Sector_name, CaseClassifications/Actor/SectorAllocRegio/Sector_name, CaseClassifications/Actor/SectorAllocRegio/allocregio_name);
			
			attribute<Jobs6>  Jobs6_rel            := rlookup(SubSector_name, Jobs6/name);
			
			
			container V := for_each_nedv(label, string(id(.))+'[..]', void, .);
		}
		unit<uint8> XSubsector_uint8 := range(uint8, 0b, uint8(#XSubsector))
		{
			attribute<uint32> BrushColor := union_data(.,  XSubsector/BrushColor);
		}
		
		unit<uint32> Wonen_x_Werken := ='union_unit('+Subsector_ref[V/Wonen]+','+Subsector_ref[V/Werken]+')'
		{
			attribute<string> subsector_name := ='union_data(., '+Subsector_ref[V/Wonen]+'/name,'+Subsector_ref[V/Werken]+'/name)';
			attribute<string> sector_name    := ='union_data(., const(''Wonen'','+Subsector_ref[V/Wonen]+'), const(''Werken'','+Subsector_ref[V/Werken]+'))';
		}
	}


}